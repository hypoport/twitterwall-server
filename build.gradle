apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

description = 'Hypoport Twitterwall Server'
version = '0.1.0-SNAPSHOT'

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}

project.ext.springVersion = '3.2.6.RELEASE'
project.ext.logbackVersion = '1.0.13'

repositories {
    mavenLocal()
    mavenCentral()
}

jettyRunWar {
    // default settings, so that Twitterwall (frontend) developement reverse proxy tool works
    contextPath = ''
    httpPort = 8000
}

dependencies {

    compile 'org.twitter4j:twitter4j-core:3.0.5+'
    compile 'javax.inject:javax.inject:1'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'com.google.guava:guava:15.0'
    compile "org.springframework:spring-core:$project.ext.springVersion",
            "org.springframework:spring-web:$project.ext.springVersion",
            "org.springframework:spring-webmvc:$project.ext.springVersion",
            "org.springframework:spring-context:$project.ext.springVersion"

    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'

    testCompile group: 'org.testng', name: 'testng', version: '6.8.+'
    testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4+'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5+'
}

test {
    useTestNG()
}

war {
    /*
        Copy static sources from Hypoport's Twitterwall frontend into thw WAR files root folder

        You need to clone https://github.com/hypoport/twitterwall project into
        twitterwall-server parent folder. Obviously you need to build the frontend project first ;-)
     */
    into('/') {
        from '../twitterwall/dist'
    }
}
